(* 
	webrelay.8th
 
  Simple RPi web relay manager.
 
 Copyright C 2019 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

\ ========================================
\ Testing only
true var, webrelay_test
true var, webrelay_word_test
false var, webrelay_func_test

webrelay_test @ #if
	needs debug/sed
	true SED-CHECK
	\ needs debug/nicer	
	\ needs debug/trace-crash
	\ needs debug/trace
	\ ( "EXCEPT: " . . cr bye ) w:is handler
#then

\ Define our namespace
ns: webrelay

\ ====================================================================================
\ Vars

\ ====================================================================================
\ Configuration
\ 	- mainmap can have n arrays. We take the channel index pin from each array to energise or de-energise.
\		- mapon (if present) array index 0 is the channels for which we should ALWAYS energise array index 1 pins.
\		- mapoff (if present) array index 0 is the channels for which we should ALWAYS de-energise array index 1 pins.
\		- inverse if true invert the logic i.e. a logic 1 turns the device attached to the channel off.
\		- exclusive if true when energising turn all other channels to the off state before energising the given channel.

\ Called from any task. Only store config locally!
\ -- config 
: lpf_pinmap	SED: -- m
	{
		"name" : "LPF",
		"numchannels" : 6,
		"mainmap" : [[27,22,5,6,13,26],[18,23,24,25,12,16]],
		"mapon" : [[4,5,6],[4,17]],
		"inverse" : true,
		"exclusive" : true
	}
;

\ -- config 
: ports_pinmap	SED: -- m	
	{
		"name" : "Ports",
		"numchannels" : 4,
		"mainmap" : [[4,17,27,22]],
		"inverse" : false,
		"exclusive" : true
	}
;

\ -- config 
: power_pinmap	SED: -- m
	{
		"name" : "Power",
		"numchannels" : 8,
		"mainmap" : [[2,3,4,17,27,22,10,9]],
		"inverse" : true,
		"exclusive" : true
	}
;

\ ========================================
\ Get pinmap map for given name
\ name -- pinmap
: get_pinmap SED: s -- m
	{ "lpf" : ' lpf_pinmap , "ports" : ' ports_pinmap , "power" : ' power_pinmap }
	swap caseof
;

\ ===============================================================================
\ The GPIO interface runs as a separate task
\ The following words must only be called from the GPIO interface task!

\ ====================================================================================
\ Accessors for pinmap and gpiomap

\ ========================================
\ Compare channels
\ ch ch -- true|false
: ch_cmp SED:  n n -- T
	n:= if true else false then
;

\ ========================================
\ Pin map
\ -- pinmap
: pinmap	SED: -- m
		"pinmap" t:@
;

\ ========================================
\ Pin map main map
\ -- pinmap.mainmap
: pinmap_mainmap	SED: -- a
		pinmap "mainmap" m:@ nip
;

\ ========================================
\ Pins for channel in main map
\ array-to-populate channel pinmap.mainmap -- array-to-populate channel
: collect_channel	SED: a n a -- a n
	swap >r r@ a:@			\ array pinmap value
	rot swap a:push swap drop r>
;

\ channel -- pinmap_mainmap_for_channel
: pinmap_mainmap_for_channel	SED: n -- a
	n:1- a:new swap pinmap_mainmap ' collect_channel a:each!
	2drop
;

\ ========================================
\ Pin map 'on' map
\ -- on-map
: pinmap_onmap	SED: -- a
		pinmap "mapon" m:exists? if "mapon" m:@ else null then nip
;

\ ========================================
\ Pin map 'on' pins
\ SED:  -- on-map-pins | null
: pinmap_onmap_pins SED: -- *
		pinmap_onmap null? if null
		else 1 a:@ nip then
;

\ ========================================
\ Pin map 'on' channels
\ -- on-map-ch | null
: pinmap_onmap_ch	SED: -- *
		pinmap_onmap null? if null
		else 0 a:@ nip then
;

\ ========================================
\ Channel in pin map 'on' channels
\ channel -- f | null
: pinmap_onmap_ch_in	SED: n -- *
		pinmap_onmap null? if drop null
		else 
			0 a:@ nip swap ' ch_cmp a:indexof nip null? if false else true then
		then nip
;

\ ========================================
\ Pin map 'off' map
\ -- off-map | null
: pinmap_offmap	SED: -- *
		pinmap "mapoff" m:exists? if "mapoff" m:@ else null then nip
;

\ ========================================
\ Pin map 'off' pins
\ -- off-map-pins | null
: pinmap_offmap_pins	SED: -- *
		pinmap_offmap null? if drop null
		else 1 a:@ nip then
;

\ ========================================
\ Pin map 'off' channels
\ -- off-map-ch | null
: pinmap_offmap_ch	 SED: -- *	
		pinmap_offmap null? if drop null
		else 0 a:@ nip then
;

\ ========================================
\ Channel in pin map 'off' channels
\ channel -- f | null
: pinmap_offmap_ch_in	SED: -- *
		pinmap_offmap null? if drop null
		else 
			0 a:@ nip swap ' ch_cmp a:indexof null? if false else true then
		then
;

\ ========================================
\ Pin map inverse
\ -- pinmap-inverse
: pinmap_inverse	SED: -- T 
		pinmap "inverse" m:@ nip
;

\ ========================================
\ Pin map exclusive
\ -- pinmap-exclusive
: pinmap_exclusive	SED: -- T 
		pinmap "exclusive" m:@ nip
;

\ ========================================
\ GPIO map
\ -- gpiomap
: gpiomap	SED: -- m
		"gpiomap" t:@
;

\ ========================================
\ GPIO inst
\ channel -- gpiomap-inst
: gpiomap_inst	SED: s -- *
		gpiomap swap m:@ nip
;

\ ====================================================================================
\ Writers

\ ========================================
\ Low level write
: writer	\ pin state --
	hw:arm? if
		\ Real hardware
		swap gpiomap_inst swap hw:gpio!
	else
		\ Simulation
		if 1 else 0 then
		swap "Channel %d set to %d" s:strfmt log
	then
;

\ ========================================
\ Write value to GPIO/Sim according to spec
: write_pin	\ energise pin -- energise
	1 pick
	if
		\ State 'on'
		pinmap_inverse if false writer else true writer then
	else
		\ State 'off'
		pinmap_inverse if true writer else false writer then
	then
;

\ ========================================
: write_pins	\ pin-array --
	false swap ' write_pin a:each! 2drop
;

\ ========================================
\ Write all values for the given channel
: write_all	\ channel energise --
		\ Retrieve main pinmap and process each pin array
		pinmap_exclusive if
			\ Turn all relays off first
			pinmap_mainmap ' write_pins a:each! drop
		then
		swap >r r@ pinmap_mainmap_for_channel ' write_pin a:each! drop
		\ See if we have any in the 'on' section
		r@ pinmap_onmap_ch_in null? not if
			\ Something to do
			pinmap_onmap_pins ' write_pin a:each! drop
		then
		\ See if we have any in the 'off' section
		pinmap_offmap_ch_in null? not if
			\ Something to do
			pinmap_onmap_pins ' write_pin a:each! drop
		then
;

\ ====================================================================================
\ Initialisation

\ ========================================
: init_pin	\ pin --
	hw:arm? if
		\ If running on ARM hardware create a gpio instance
		\ and store in the gpiomap at key = pin
		"gpiomap" t:@ swap false hw:gpio m:! drop false swap write_pin
	else
		\ Simulation
		"gpiomap" t:@ 1 pick null m:! drop false swap write_pin drop
	then
;

\ ========================================
: init_pins	\ pin-array --
	' init_pin a:each! drop
;

\ ========================================
\ Pin initialisation
: init_all 	\ --
	\ Retrieve main pinmap and process each pin array
	pinmap_mainmap ' init_pins a:each! drop
	\ Retrieve ancilliary pinmaps and process the pin array
	pinmap_onmap_pins null? not if ' init_pin a:each! then drop
	pinmap_offmap_pins null? not if ' init_pin a:each! then drop
;

\ ===============================================================================
\ Execution words
\ ToDo: Why does each execution word add 0 to the stack when not even called.
\========================================
\ Quit
: do_quit \ _ -- f
	drop true
;

\========================================
\ Energise pin
: do_on	\ channel -- f
	true write_all
;

\========================================
\ Deenergise pin
: do_off	\ channel -- f
	false write_all
;

\========================================
\ Message process
: msg_pump	\ --
	\ Process requests until quit message
	repeat
		\ Wait for work
		-1 t:q-wait
		t:pop null? if
			"Got null from Q!" log
		else
			a:open swap \ channel cmd
			\ Execute the command
			{ "quit" : ( do_quit ) , "on" : ( do_on ) , "off" : ( do_off ) }
			swap caseof if break then	
		then
	again
;

\ ===============================================================================
\ The GPIO task word

\ ========================================
\ The task entry point
: GPIO_interface	\ module-type --
	\ Set task name and prevent throwing on q empty
	"GPIO-Interface" t:name!
	getq false q:throwing drop
	cr "GPIO_interface task waiting for requests..." . cr
	\ Retrieve our configuration, save to task var
	get_pinmap "pinmap" t:!
	\ Create a map to hold the gpio instances, save to task var
	m:new "gpiomap" t:!
	\ Initialise interface
	init_all
	\ Process requests
	msg_pump
	cr "GPIO_interface task exiting..." .
;

\ ========================================
\ Reset namespace
ns: user

\ ===============================================================================
\ ===============================================================================
\ Testing

webrelay_test @ #if
	ns: webrelay
	webrelay_word_test @ #if
		\ ========================================
		\ Test accessors
		\ Pin map
		"Test accessors..." . cr
		"lpf" get_pinmap "pinmap" t:! drop
		pinmap . cr
		pinmap_mainmap . cr
		4 pinmap_mainmap_for_channel . cr
		pinmap_onmap . cr
		pinmap_onmap_pins . cr
		pinmap_onmap_ch . cr
		4 pinmap_onmap_ch_in . cr
		pinmap_offmap . cr
		pinmap_offmap_pins . cr
		pinmap_offmap_ch . cr
		4 pinmap_offmap_ch_in . cr
		pinmap_inverse . cr
		pinmap_exclusive . cr

		\ GPIO map
		"Test GPIO map" . cr
		{"1" : "Inst-1", "2" : "Inst-2" } "gpiomap" t:! drop
		gpiomap . cr
		"1" gpiomap_inst . cr
		"2" gpiomap_inst . cr
		1 sleep

		\ ========================================
		\ Test writers
		"Test writers" . cr
		6 true write_all cr
		1 false write_all cr
		1 sleep
	#then

	webrelay_func_test @ #if
		\ ========================================
		\ Test simulation
		"Test simulation" . cr
		"lpf" 1 ' GPIO_interface t:task-n
		2 sleep
		dup ["on", 5] t:push t:q-notify
		dup ["quit", null] t:push t:q-notify
		1 sleep
	#then

	bye
#then

